dnl	Run autoconf on this file to produce the system configuration
dnl	script "configure"

# FileName	[ configure.in ]
#
# PackageName	[ mocha ]
#
# Synopsis	[ System configuration script for autoconf ]
#
# SeeAlso	[ Makefile.in ]
#
# Author	[ Stephen Edwards <sedwards@eecs.berkeley.edu> ]
#
# Copyright	[
#  Copyright (c) 1994-1996 The Regents of the Univ. of California.
#  All rights reserved.
#
#  Permission is hereby granted, without written agreement and without license
#  or royalty fees, to use, copy, modify, and distribute this software and its
#  documentation for any purpose, provided that the above copyright notice and
#  the following two paragraphs appear in all copies of this software.
#
#  IN NO EVENT SHALL THE UNIVERSITY OF CALIFORNIA BE LIABLE TO ANY PARTY FOR
#  DIRECT, INDIRECT, SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES ARISING OUT
#  OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF THE UNIVERSITY OF
#  CALIFORNIA HAS BEEN ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
#
#  THE UNIVERSITY OF CALIFORNIA SPECIFICALLY DISCLAIMS ANY WARRANTIES,
#  INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND
#  FITNESS FOR A PARTICULAR PURPOSE.  THE SOFTWARE PROVIDED HEREUNDER IS ON AN
#  "AS IS" BASIS, AND THE UNIVERSITY OF CALIFORNIA HAS NO OBLIGATION TO PROVIDE
#  MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS, OR MODIFICATIONS.
#  ]
#
# Revision	[$Id: configure.in,v 1.3 2003/05/24 07:07:39 vaibhav Exp $]

AC_INIT(src/img/img.make)

# Give the configurer a chance to set the location of the GLU tree

AC_SUBST(glulibdir)
AC_ARG_WITH(glu-libdir,
[  --with-glu-libdir=<libdir> Specify directories to search for the GLU library
			  (defaults to ../glu-chai and ../../glu-chai).
                          Expects to find <rootdir>/libglu.a],
[glulibdir=$withval], [glulibdir="../glu-chai ../../glu-chai"])

AC_SUBST(gluincdir)
AC_ARG_WITH(glu-incdir,
[  --with-glu-incdir=<incdir> Specify directories to search for the GLU headers
			  (defaults to ../glu-chai/include and
                          ../../glu-chai/include).],
[gluincdir=$withval], [gluincdir="../glu-chai/include ../../glu-chai/include"])

# Give the configurer a chance to set a different location for the MOCHA
# headers and library

AC_SUBST(mochalibdir)
AC_ARG_WITH(mocha-libdir,
[  --with-mocha-libdir=<libdir> Specify directories to search for the MOCHA
			  and headers.  Defaults to \".\".
                          Expects to find <libdir>/libmocha.a],
[mochalibdir=$withval],
[mochalibdir="."])

# Give the configurer a chance to set a different location for the MOCHA
# source.  When specified, "srcdir" points to "master" source, and
# "local_srcdir" points to the source under local development.

AC_SUBST(local_srcdir)
AC_ARG_WITH(local-srcdir,
[  --with-local-srcdir=<srcdir> Specify the root directory to search for
			  source for packages (the PKGS list).
			  Expects to find, e.g., <srcdir>/tbl/tbl.c],
[local_srcdir=$withval],
[local_srcdir=$srcdir])

# Give the configurer a chance to set the BDD library

AC_SUBST(BDDLIB)
AC_ARG_WITH(bdd,
[  --with-bdd=<bddPackage> Specify the BDD library to link against.
                          Defaults to --with-bdd=cu.],
[BDDLIB=-l$withval],
[BDDLIB=-lcu])

#----------------------------------------------------------------------
# Checks for programs we need
#----------------------------------------------------------------------
AC_PROG_CC

AC_ARG_ENABLE(64,
  [  --enable-64             Use 64-bit pointers on 64-bit Alpha machines],
  [use_sixty_four=$enableval], [use_sixty_four=no])

# echo "The value of use_sixty_four is $use_sixty_four"

AC_ARG_WITH(comp-mode,
[  --with-comp-mode=<mode> Specify a special compilation mode:
			  optimize (the default) : -O -NDEBUG (no assert()s)
			  debug: -g
			  purify: Compile and link with purify
			  quantify: Compile and link with quantify],
[comp_mode=$withval],
[comp_mode=optimize])

AC_SUBST(LINKER)

LINKER="$CC"

case "$comp_mode" in
  debug)
	CFLAGS=-g ;;
  purify)
	LINKER="purify -cache-dir=/tmp $CC"
	CFLAGS=-g ;;
  quantify)
	LINKER="quantify -cache-dir=/tmp $CC"
	AC_DEFINE(QUANTIFY)
	CFLAGS=-g ;;
  optimize | * )
	if test $ac_cv_prog_gcc = yes; then
	    CFLAGS="-g -O3"
	elif test "${CFLAGS+set}" != set; then
            CFLAGS="-O"
	fi
	AC_DEFINE(NDEBUG) ;;
esac


AC_MSG_CHECKING([system version (for dynamic loading)])
if test -f /usr/lib/NextStep/software_version; then
    system=NEXTSTEP-`awk '/3/,/3/' /usr/lib/NextStep/software_version`
else
    system=`uname -s`-`uname -r`
    if test "$?" -ne 0 ; then
	AC_MSG_RESULT([unknown (can't find uname command)])
	system=unknown
    else
	# Special check for weird MP-RAS system (uname returns weird
	# results, and the version is kept in special file).
    
	if test -r /etc/.relid -a "X`uname -n`" = "X`uname -s`" ; then
	    system=MP-RAS-`awk '{print $3}' /etc/.relid'`
	fi
	if test "`uname -s`" = "AIX" ; then
	    system=AIX-`uname -v`.`uname -r`
	fi
	AC_MSG_RESULT($system)
    fi
fi

#   on the alphas OSF1-V4.0
#   Solaris on sparc SunOS-5.5.1 (icw)
#   HP-UX-B.10.20 (parker.eecs) HPUX
#   solaris on x86 po.eecs SunOS-5.6
# 	Linux-2.0.33
# ULTRIX-4.5

if test $comp_mode = optimize ; then 
case $system in 
	SunOS-5*)
	if test $ac_cv_prog_gcc = no; then
		CFLAGS="-xO5 -native -dalign"	
	fi

	;;
	ULTRIX-4.*)
	if test $ac_cv_prog_gcc = no; then
		CFLAGS="-std1 -O -Olimit 5000"
	fi
	;;
	Linux*)
	if test $ac_cv_prog_gcc = yes; then
	    CFLAGS="-g -O3"
	elif test "${CFLAGS+set}" != set; then
            CFLAGS="-O"
	fi
	;;
	OSF*)
	CC="cc"
	LINKER="cc"
	CFLAGS="-g3 -O4 -std -ieee_with_no_inexact -tune host"
#							      -xtaso needed here?
	if test "$use_sixty_four" = "no"; then
          CFLAGS="$CFLAGS -xtaso"
#	  AC_DEFINE(SIZEOF_VOID_P, 4)
#           ac_sizeof_voidp=4
        fi	
	LDFLAGS="-taso"
	;;

esac
fi


AC_PROG_RANLIB
AC_PROG_INSTALL

AC_PROG_LEX
AC_MSG_CHECKING(if $LEX accepts the -o and -P options)

AC_CACHE_VAL(ac_cv_flex_accepts_op,
[ ac_cv_flex_accepts_op=yes ;
echo "%%\
%%" | $LEX -Ptest -o/dev/null >/dev/null 2>&1 || ac_cv_flex_accepts_op=no ])
if test $ac_cv_flex_accepts_op = yes ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_WARN([You either need a newer version of flex, or need to modify the defintion of LEX in the Makefile to point to a version that does accept -p -t and -o.])
fi

AC_PROG_YACC
AC_MSG_CHECKING([if $YACC accepts the -p, -t, and -o options])
AC_CACHE_VAL(ac_cv_yacc_accepts_pto,
[ ac_cv_yacc_accepts_pto=yes ;
echo "%token terminal\
%%\
nonterminal: terminal\
%%" > config.in
$YACC -ptest -o /dev/null config.in >/dev/null 2>&1 || ac_cv_yacc_accepts_pto=no
rm -f config.in ])
if test $ac_cv_yacc_accepts_pto = yes ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_WARN([You either need a newer version of bison, or need to modify the defintion of YACC in the Makefile to point to a version that does accept -p -t and -o.])
fi

AC_CHECK_PROG(PERL,perl5,perl5,perl)

#----------------------------------------------------------------------
# Checks for headers and libraries
#----------------------------------------------------------------------
AC_HEADER_STDC

#the extra libs are going to be collected in this variable
EXTRALIBS=""

AC_SUBST(EXTRALIBS)
# Step 2: check for existence of -ldl library.  This is needed because
# Linux can use either -ldl or -ldld for dynamic loading.

# -ldl for linux and solaris
# -lsocket -lnsl -rpcsvc for solaris

AC_CHECK_LIB(socket, getsockname, [EXTRALIBS="$EXTRALIBS -lsocket"])
AC_CHECK_LIB(nsl, gethostbyname, [EXTRALIBS="$EXTRALIBS -lnsl"])
AC_CHECK_LIB(rpcsvc, xdr_sprayarr, [EXTRALIBS="$EXTRALIBS -lrpcsvc"])
AC_CHECK_LIB(dl, dlopen, [EXTRALIBS="$EXTRALIBS -ldl"])


# Check for X libraries and header files
AC_SUBST(INCDIRS)
AC_SUBST(LIBDIRS)
AC_SUBST(LEXLIB) 

# AC_PAT_X sets x_includes and x_libraries only when they have not been defined previosly.
AC_PATH_X

# The tcl directory is given as environment variable.

# Take care of the header paths.
if test -n "$x_includes"; then
  INCDIRS="$INCDIRS -I$x_includes"
fi

if test -n "$tcl_header_path"; then
  INCDIRS="$INCDIRS -I$tcl_header_path"
fi

# Take care of the library paths
if test -n "$x_libraries"; then
  LIBDIRS="$LIBDIRS -L$x_libraries"
fi

if test -n "$tcl_library_path"; then
  LIBDIRS="$LIBDIRS -L$tcl_library_path"
fi

# flex library path
if test -n "$flex_library_path"; then
  LIBDIRS="$LIBDIRS -L$flex_library_path"
  LEXLIB=-lfl
else 
  if test $ac_cv_prog_LEX = flex; then 
   AC_PATH_PROG(FLEXPATH, flex)
#    echo "Flex's path is $FLEXPATH"
    FLEXLIBPATH=-L`echo $FLEXPATH | sed 's/bin\/flex/lib/g'`
    AC_SUBST(FLEXLIBPATH)
#     echo $FLEXLIBPATH
   LEXLIB=-lfl
   fi
fi


#----------------------------------------------------------------------
# Checks for typedefs, structures, and compiler characteristics.
#----------------------------------------------------------------------

# Check to see if the compiler understands "const"
# #define it empty otherwise
AC_C_CONST

#----------------------------------------------------------------------
# Checks the size of pointers and ints
#----------------------------------------------------------------------

echo $ac_n "setting size of void *""... $ac_c" 1>&6
if test "$use_sixty_four" = "no"; then
	  AC_DEFINE(SIZEOF_VOID_P, 4)
          ac_cv_sizeof_void_p=4
# 	 echo "using 32"
else 
	  AC_DEFINE(SIZEOF_VOID_P, 8)
          ac_cv_sizeof_void_p=8
# 	echo "using 64"
fi
echo "$ac_t""$ac_cv_sizeof_void_p" 1>&6

# if test "$use_sixty_four" = "no"; then
# echo $ac_n "setting size of void *""... $ac_c" 1>&6
# ac_cv_sizeof_void_p=4
# echo "$ac_t""$ac_cv_sizeof_void_p" 1>&6
# cat >> confdefs.h <<EOF
# #define SIZEOF_VOID_P $ac_cv_sizeof_void_p
# echo "I am within the 32 bit section" 
# EOF
# else
# echo "should be using 64" 
# AC_CHECK_SIZEOF(void *)
# fi


AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)

#----------------------------------------------------------------------
# Checks for library functions.
#----------------------------------------------------------------------

AC_CHECK_FUNCS(strchr strstr)

#----------------------------------------------------------------------
# Create the Makefile from Makefile.in
#----------------------------------------------------------------------
AC_OUTPUT(Makefile)

